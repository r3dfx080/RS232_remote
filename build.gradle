plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'org.foxprod'
version '0.5'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.2'
}

sourceCompatibility = '21'
targetCompatibility = '21'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'org.foxprod.rs232_remote'
    mainClass = 'org.foxprod.rs232_remote.Main'
}

jar {
    manifest {
        attributes "Main-Class": 'org.foxprod.rs232_remote.Main'
    }
}
javafx {
    version = '21'
    modules = ['javafx.base','javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation("com.fazecast:jSerialComm:[2.0.0,3.0.0)")
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    jpackage {
        icon = "$rootDir/src/main/resources/org/foxprod/rs232_remote/icon.ico"
        appVersion = getAppVersion();
        installerType = "exe"
    }
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    targetPlatform("win") {
    }
    options = ['--strip-debug', '--no-header-files', '--no-man-pages']

    launcher {
        name = 'RS232 remote'
    }
}

tasks.jpackage.doLast {
    copy {
        from project.projectDir
        include "config.properties"
        into "$buildDir/jpackage/${jlink.launcherData.get().name}"
    }
}

jlinkZip {
    group = 'distribution'
}